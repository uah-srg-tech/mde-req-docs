library docptl2doc;

modeltype doc uses "http://srg.aut.uah.es/micobs/doc/doc";
modeltype doctpl uses "http://srg.aut.uah.es/micobs/doctpl/doctpl";

mapping doctpl::DAbstractRelatedDocument::GenerateADRDs() : doc::DDBody {
	init {
		result := object DDBody {
			bodyContent += self.map GenerateADRD();
		}
	}
}

mapping doctpl::DAbstractRelatedDocument::GenerateADRD() : doctpl::DBodyContent {
	init {
		result := object DParagraph {
			name := self.name;
			style := "ADRD";
			paragraphContent += object DRun {
				text := object DText {
					content := self.name;
				}
			};
			paragraphContent += object DRun {
				tab := object DTab {};
				italics := DRunAttributes::TRUE;
				text := object DText {
					content := self.title;
				}
			};
			paragraphContent += object DRun {
				text := object DText {
					content := ". " + self.id;
					if(self.issue <> null)
						content := content + " issue " + self.issue.toString();
					if(self.revision <> null)
						content := content + " rev " + self.revision.toString();
					if(self.date <> null)
						content := content + ". " + self.date;
				}
			}
		}
	}
}

mapping doctpl::DBody::GenerateBody() : doc::DDBody {
	result.bodyContent += self.bodyContent->map GenerateBodyContent();
}

mapping doctpl::DBodyContent::GenerateBodyContent() : doctpl::DBodyContent {
	init {
		if(self.metaClassName() = "DParagraph") {
			result := self.oclAsType(DParagraph).map GenerateParagraph();
		}
		else if(self.metaClassName() = "DFigureFromFile") {
			result := self.oclAsType(DFigureFromFile).map GenerateFigureFromFile();
		}
		else if(self.metaClassName() = "DTableFromFile") {
			result := self.oclAsType(DTableFromFile).map GenerateTableFromFile();
		}
		else if(self.metaClassName() = "DBasicTable") {
			result := self.oclAsType(DBasicTable).map GenerateBasicTable();
		}
		else if(self.metaClassName() = "DEnumerate") {
			result := self.oclAsType(DEnumerate).map GenerateEnumerate();
		}
		else if(self.metaClassName() = "DItemize") {
			result := self.oclAsType(DItemize).map GenerateItemize();
		}
	}
	result.style := self.style;
	if(self.alignment <> doctpl::DAlignment::left) {
		result.alignment := self.alignment;
	};
	result.indent := self.indent;
}

mapping doctpl::DParagraph::GenerateParagraph() : doctpl::DParagraph {
	if(self.name <> null) {
		result.name := self.name;	
	};
	var i : Integer := 1;
	while(i <= self.paragraphContent->size()) {
		if(self.paragraphContent->at(i).metaClassName() = "DRun") {
			result.paragraphContent += self.paragraphContent->at(i).oclAsType(DRun).map GenerateRun();
		}
		else if(self.paragraphContent->at(i).metaClassName() = "DHyperlink") {
			result.paragraphContent += self.paragraphContent->at(i).oclAsType(DHyperlink).map GenerateHyperlink();
		};
		i := i + 1;
	}
}

mapping doctpl::DRun::GenerateRun() : doctpl::DRun {
	result.bold := self.bold;
	result.italics := self.italics;
	result.underline := self.underline;
	if(self.tab <> null) {
		result.tab := object DTab {};
	};
	result.text := object DText {
		content := self.text.content.substring(7, self.text.content.size()-7);
	}
}

mapping doctpl::DHyperlink::GenerateHyperlink() : doctpl::DHyperlink {
	result.reference := self.reference.late resolveone(DReferenceableObject);
	if(self.run = null) {
		result.run := object DRun {
			text := object DText {
				content := self.reference.name;
			}
		}
	} else {
		result.run := self.run.map GenerateRun();
	};
	result.run.italics := DRunAttributes::TRUE;
}

mapping doctpl::DFigureFromFile::GenerateFigureFromFile() : doctpl::DFigureFromFile {
	result.name := self.name;
	result.referenceFile := self.referenceFile;
	result.width := self.width;
	result.height := self.height;
	result.caption := self.caption;
}

mapping doctpl::DTableFromFile::GenerateTableFromFile() : doctpl::DTableFromFile {
	result.name := self.name;
	result.referenceFile := self.referenceFile;
	result.width := self.width;
	result.height := self.height;
	result.caption := self.caption;
}

mapping doctpl::DBasicTable::GenerateBasicTable() : doctpl::DBasicTable { 
	result.name := self.name;
	result.width := self.width;
	result.caption := self.caption;
	result.rows += self.rows.map GenerateRows();
}

mapping doctpl::DRow::GenerateRows() : doctpl::DRow {
	result.cells += self.cells.map GenerateCells();
}

mapping doctpl::DCell::GenerateCells() : doctpl::DCell {
	if(self.colSpan <> null)
		result.colSpan := self.colSpan;
	if(self.rowSpan <> null)
		result.rowSpan := self.rowSpan;
	if(self.width <> null)
		result.width := self.width;
	if(self.shadow <> null)
		result.shadow := self.shadow;
	
	var i : Integer := 1;
	while(i <= self.bodyContent->size()) {
		result.bodyContent += self.bodyContent.map GenerateBodyContent();
		i := i + 1;
	}
}

mapping doctpl::DEnumerate::GenerateEnumerate() : doctpl::DEnumerate {
	if(self.name <> null) {
		result.name := self.name;	
	};
	if(self.style <> null) {
		result.style := self.style;
	};
	if(self.alignment <> doctpl::DAlignment::left) {
		result.alignment := self.alignment;
	};
	result.items += self.items.map GenerateItems();
}

mapping doctpl::DItemize::GenerateItemize() : doctpl::DItemize {
	if(self.name <> null) {
		result.name := self.name;	
	};
	if(self.style <> null) {
		result.style := self.style;
	};
	if(self.alignment <> doctpl::DAlignment::left) {
		result.alignment := self.alignment;
	};
	result.items += self.items.map GenerateItems();
}

mapping doctpl::DListItem::GenerateItems() : doctpl::DListItem {
	if(self.paragraph <> null) {
		result.paragraph := self.paragraph.map GenerateParagraph();
	};
	if(self.sublist <> null) {
		if(self.sublist.metaClassName() = "DEnumerate") { 
			result.sublist := self.sublist.oclAsType(DEnumerate).map GenerateEnumerate();
		} else if(self.sublist.metaClassName() = "DItemize") {
			result.sublist := self.sublist.oclAsType(DItemize).map GenerateItemize();
		}
	}
}

mapping doctpl::DTBC::GenerateTBCs() : doc::DDBody {
	init {
		result := object DDBody {
			bodyContent += self.map GenerateTBC();
		}
	}
}

mapping doctpl::DTBC::GenerateTBC() : doctpl::DBodyContent {
	init {
		result := object DParagraph {
			name := self.name;
			paragraphContent += self.GenerateAbstractPlaceholder();
			if(self.parentTBC != null) {
				paragraphContent += object DRun {
					italics := DRunAttributes::TRUE;
					text := object DText {
						content := self.parentTBC.name;
					}
				};
				paragraphContent += object DRun {
					text := object DText {
						content := " at ";
					}
				};
				var parentDoc : DDocumentTemplate := self.parentTBC.container().container().oclAsType(DDocumentTemplate);
				paragraphContent += object DRun {
					italics := DRunAttributes::TRUE;
					text := object DText {
						content := parentDoc.name;
					}
				};
				paragraphContent += object DRun {
					text := object DText {
						content := " (" + parentDoc.id + ")" ;
					}
				}
			}
		}
	}
}

mapping doctpl::DTBD::GenerateTBDs() : doc::DDBody {
	init {
		result := object DDBody {
			bodyContent += self.map GenerateTBD();
		}
	}
}

mapping doctpl::DTBD::GenerateTBD() : doctpl::DBodyContent {
	init {
		result := object DParagraph {
			name := self.name;
			paragraphContent += self.GenerateAbstractPlaceholder();
			if(self.parentTBD != null) {
				paragraphContent += object DRun {
					italics := DRunAttributes::TRUE;
					text := object DText {
						content := self.parentTBD.name;
					}
				};
				paragraphContent += object DRun {
					text := object DText {
						content := " at ";
					}
				};
				var parentDoc : DDocumentTemplate := self.parentTBD.container().container().oclAsType(DDocumentTemplate);
				paragraphContent += object DRun {
					italics := DRunAttributes::TRUE;
					text := object DText {
						content := parentDoc.name;
					}
				};
				paragraphContent += object DRun {
					text := object DText {
						content := " (" + parentDoc.id + ")" ;
					}
				}
			}
		}
	}
}

helper doctpl::DAbstractPlaceholder::GenerateAbstractPlaceholder() : OrderedSet(DParagraphContent) {
	var info := object OrderedSet(DParagraphContent) {}; 
	info += object DRun {
		text := object DText {
			italics := DRunAttributes::TRUE;
			content := self.name;
		}
	};
	info += object DRun {
		text := object DText {
			content := ": ";
		}
	};
	if(self.description != null) {
		info += object DRun {
			text := object DText {
				content := self.description;
			}
		}
	};
	return info;
}